// Copyright © Advanced Micro Devices, Inc. All rights reserved.
//
// MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// CCO p2p get bandwidth — PE 0 pulls from PE 1's send window into its recv
// window.
//
//   -t lsa   : intra-node flat-VA load loop (read peer slot → local).
//   -t sdma  : intra-node copy engine via ccoSdma.
//   -t ibgda : cross-node one-sided RDMA read via ccoGda<PrvdType>.

#include <cstdio>
#include <cstdlib>
#include <vector>

#include "device_utils.hpp"
#include "hip/hip_runtime.h"
#include "mori/application/utils/check.hpp"
#include "mori/cco/cco_scale_out.hpp"
#include "util.hpp"

namespace mori::cco::benchmark {

// LSA: flat-VA load loop (peer send → local recv). scope_size = copy
// granularity; all block threads participate (see p2p_put_bw).
__global__ void lsa_get_bw(ccoWindowDevice* sendWin, ccoWindowDevice* recvWin,
                           volatile unsigned int* counter_d, size_t len_doubles, int peerLsa,
                           int iter, int scope_size) {
  const int bid = blockIdx.x;
  const int nblocks = gridDim.x;
  const int tid = linear_tid();
  const int nthreads = blockDim.x * blockDim.y * blockDim.z;

  const size_t chunk = len_doubles / static_cast<size_t>(nblocks);
  const int nunits = nthreads / scope_size;
  const int unit = tid / scope_size;
  const int lane = tid % scope_size;
  const size_t per_unit = chunk / static_cast<size_t>(nunits);

  const size_t off_bytes =
      (static_cast<size_t>(bid) * chunk + static_cast<size_t>(unit) * per_unit) * sizeof(double);
  const double* src =
      reinterpret_cast<const double*>(ccoGetLsaPeerPtr(sendWin, peerLsa, off_bytes));
  double* dst = reinterpret_cast<double*>(ccoGetLocalPtr(recvWin, off_bytes));

  for (int i = 0; i < iter; i++) {
    lsa_copy_strided(dst, src, per_unit, lane, scope_size);
    __threadfence_system();  // see p2p_put_bw (cache absorption)
    bw_cross_block_barrier_round(counter_d, nblocks, i);
  }
}

// IBGDA: one QP per block; pipeline reads + flush own QP. block scope = one bulk
// read; warp/thread subdivide (see p2p_put_bw).
template <core::ProviderType PrvdType, typename Coop,
          ccoGdaThreadMode ThreadMode = ccoGdaThreadIndependent>
__global__ void ibgda_get_bw(ccoWindowDevice* sendWin, ccoWindowDevice* recvWin, size_t len_doubles,
                             ccoDevComm devComm, int iter) {
  Coop coop;
  const int bid = blockIdx.x;
  const int nblocks = gridDim.x;
  ccoGda<PrvdType> gda{devComm, /*ginContext=*/bid};  // one QP context per block
  const int peer = !devComm.rank;
  const size_t chunk = len_doubles / static_cast<size_t>(nblocks);

  const int tid = linear_tid();
  const int unit = tid / coop.size();
  const int nunits = (blockDim.x * blockDim.y * blockDim.z) / coop.size();
  const size_t per_unit = chunk / static_cast<size_t>(nunits);
  const size_t base = static_cast<size_t>(bid) * chunk + static_cast<size_t>(unit) * per_unit;
  const size_t off_bytes = base * sizeof(double);
  const size_t bytes = per_unit * sizeof(double);

  // Per-op doorbell: per-op flow control drains completions as the SQ fills
  // (see p2p_put_bw). The trailing flush waits for the last ops before timing.
  for (int i = 0; i < iter; i++) {
    gda.template get<CCO_TEAM_WORLD, ThreadMode>(peer, reinterpret_cast<ccoWindow_t>(sendWin),
                                                 off_bytes, reinterpret_cast<ccoWindow_t>(recvWin),
                                                 off_bytes, bytes, coop);
  }
  gda.flush(ccoCoopBlock{});
}

// SDMA: the buffer is split over issue units -- a unit is a thread, a wavefront or
// a whole block, per Coop -- spread across the grid, one queue per unit round
// robin. Each unit's slice is issued as `depth` sub-copies: with agg the doorbell
// is suppressed and one commit() rings the batch, otherwise each sub-copy rings.
template <typename Coop, uint32_t Flags>
__global__ void sdma_get_bw(ccoWindowDevice* sendWin, ccoWindowDevice* recvWin, size_t len_doubles,
                            ccoDevComm devComm, int peerLsa, int iter, int depth) {
  ccoSdma sdma{devComm};
  // Uniform inside a coop group, so the group derives one slice and calls get together.
  int unitsPerBlock, unitInBlock;
  if constexpr (std::is_same_v<Coop, ccoCoopThread>) {
    unitsPerBlock = blockDim.x;
    unitInBlock = threadIdx.x;
  } else if constexpr (std::is_same_v<Coop, ccoCoopWarp>) {
    unitsPerBlock = blockDim.x / warpSize;
    unitInBlock = threadIdx.x / warpSize;
  } else {
    unitsPerBlock = 1;
    unitInBlock = 0;
  }
  const int nUnits = gridDim.x * unitsPerBlock;
  const int u = blockIdx.x * unitsPerBlock + unitInBlock;
  const int q = u % devComm.sdma.sdmaNumQueue;

  const size_t total = len_doubles * sizeof(double);
  const size_t per = (total / static_cast<size_t>(nUnits)) & ~size_t{7};
  size_t base = 0, bytes = total;
  if (per != 0) {
    base = static_cast<size_t>(u) * per;
    bytes = (u == nUnits - 1) ? (total - base) : per;
  } else if (u != 0) {
    return;  // more units than 8B chunks: unit 0 moves it all
  }
  if (bytes == 0) return;

  constexpr bool agg = (Flags & ccoSdmaOptFlagsAggregate) != 0;
  const size_t sub = bytes / static_cast<size_t>(depth);
  for (int i = 0; i < iter; i++) {
    for (int j = 0; j < depth; j++) {
      const size_t so = static_cast<size_t>(j) * sub;
      const size_t sb = (j == depth - 1) ? (bytes - so) : sub;
      if (sb == 0) continue;  // bytes < depth: skip empty sub-copies (no 0-byte packet)
      sdma.get<Coop, false, false, Flags>(peerLsa, reinterpret_cast<ccoWindow_t>(recvWin),
                                          base + so, reinterpret_cast<ccoWindow_t>(sendWin),
                                          base + so, sb, q);
    }
    if constexpr (agg) sdma.commit<Coop>(peerLsa, q);
  }
  sdma.quietQueue<Coop>(peerLsa, q);
}

static void launch_sdma(PutScope scope, ccoWindow_t sendWin, ccoWindow_t recvWin,
                        size_t len_doubles, ccoDevComm devComm, int peerLsa, int count,
                        int warp_size, int depth, bool agg, int nblocks, int threads) {
  const int nq = devComm.sdma.sdmaNumQueue;
  // Without -c / -T: one block, and one issue unit per queue for thread scope.
  const dim3 grid(nblocks > 0 ? nblocks : 1);
  const dim3 block(threads > 0 ? threads
                               : (scope == PutScope::kWarp    ? warp_size
                                  : scope == PutScope::kBlock ? 256
                                                              : nq));
#define LAUNCH(COOP, FLAGS)                                                                        \
  hipLaunchKernelGGL((sdma_get_bw<COOP, FLAGS>), grid, block, 0, 0, sendWin, recvWin, len_doubles, \
                     devComm, peerLsa, count, depth)
#define LAUNCH_SCOPE(FLAGS)             \
  do {                                  \
    if (scope == PutScope::kWarp)       \
      LAUNCH(ccoCoopWarp, FLAGS);       \
    else if (scope == PutScope::kBlock) \
      LAUNCH(ccoCoopBlock, FLAGS);      \
    else                                \
      LAUNCH(ccoCoopThread, FLAGS);     \
  } while (0)
  if (agg)
    LAUNCH_SCOPE(ccoSdmaOptFlagsAggregate);
  else
    LAUNCH_SCOPE(ccoSdmaOptFlagsDefault);
#undef LAUNCH_SCOPE
#undef LAUNCH
}

static void launch_lsa(PutScope scope, dim3 grid, dim3 block, ccoWindow_t sendWin,
                       ccoWindow_t recvWin, unsigned int* counter_d, size_t len_doubles,
                       int peerLsa, int count, int warp_size) {
  int scope_size = block.x;
  if (scope == PutScope::kWarp) scope_size = warp_size;
  if (scope == PutScope::kThread || scope == PutScope::kThreadAgg) scope_size = 1;
  hipLaunchKernelGGL(lsa_get_bw, grid, block, 0, 0, sendWin, recvWin, counter_d, len_doubles,
                     peerLsa, count, scope_size);
}

template <core::ProviderType PrvdType>
static void launch_ibgda(PutScope scope, dim3 grid, dim3 block, ccoWindow_t sendWin,
                         ccoWindow_t recvWin, size_t len_doubles, ccoDevComm devComm, int count) {
  switch (scope) {
    case PutScope::kBlock:
      hipLaunchKernelGGL((ibgda_get_bw<PrvdType, ccoCoopBlock>), grid, block, 0, 0, sendWin,
                         recvWin, len_doubles, devComm, count);
      break;
    case PutScope::kWarp:
      hipLaunchKernelGGL((ibgda_get_bw<PrvdType, ccoCoopWarp>), grid, block, 0, 0, sendWin, recvWin,
                         len_doubles, devComm, count);
      break;
    case PutScope::kThread:
      hipLaunchKernelGGL((ibgda_get_bw<PrvdType, ccoCoopThread>), grid, block, 0, 0, sendWin,
                         recvWin, len_doubles, devComm, count);
      break;
    case PutScope::kThreadAgg:
      hipLaunchKernelGGL((ibgda_get_bw<PrvdType, ccoCoopThread, ccoGdaThreadAggregate>), grid,
                         block, 0, 0, sendWin, recvWin, len_doubles, devComm, count);
      break;
  }
}

}  // namespace mori::cco::benchmark

int main(int argc, char** argv) {
  using namespace mori::cco;
  using namespace mori::cco::benchmark;

  PerfContext ctx{};
  const int init_rc = PerfInit(argc, argv, &ctx);
  if (init_rc != 0) {
    return init_rc == 2 ? 0 : 1;
  }

  PerfArgs& args = ctx.args;
  const int my_pe = ctx.my_pe;
  const bool run_kernels = (my_pe == 0);  // unidirectional: PE 0 pulls

  const dim3 grid(args.nblocks, 1, 1);
  const dim3 block(args.threads_per_block, 1, 1);

  PerfRes res;
  if (run_kernels) {
    PerfResAlloc(&res);
  }

  std::vector<PerfTableRow> table;
  if (my_pe == 0) {
    table.reserve(64);
  }

  for (size_t size_bytes = args.min_size; size_bytes <= args.max_size;
       size_bytes *= args.step_factor) {
    if (size_bytes % sizeof(double) != 0) continue;
    const size_t len_doubles = size_bytes / sizeof(double);

    // SDMA splits by queue count and handles uneven tails in-kernel (see put_bw).
    if (args.transport != Transport::kSdma &&
        !size_ok(args.put_scope, size_bytes, args.nblocks, args.threads_per_block,
                 ctx.device_warp_size)) {
      if (my_pe == 0) table.push_back(PerfTableRow{size_bytes, true, 0.0});
      ccoBarrierAll(ctx.comm);
      continue;
    }

    if (run_kernels) {
      const float ms = RunWarmupAndTimed(res, args.warmup, args.iters, [&](int count) {
        if (args.transport == Transport::kSdma) {
          launch_sdma(args.put_scope, ctx.send_win, ctx.recv_win, len_doubles, ctx.devComm,
                      ctx.peer_lsa_rank, count, ctx.device_warp_size, args.agg_depth,
                      args.aggregate, args.nblocks_explicit ? args.nblocks : 1,
                      args.threads_explicit ? args.threads_per_block : 0);
        } else if (args.transport == Transport::kLsa) {
          launch_lsa(args.put_scope, grid, block, ctx.send_win, ctx.recv_win, res.counter_d,
                     len_doubles, ctx.peer_lsa_rank, count, ctx.device_warp_size);
        } else {
          CCO_GDA_DISPATCH(launch_ibgda<P>(args.put_scope, grid, block, ctx.send_win, ctx.recv_win,
                                           len_doubles, ctx.devComm, count));
        }
        HIP_RUNTIME_CHECK(hipGetLastError());
      });

      const double gbps = static_cast<double>(size_bytes) /
                          (static_cast<double>(ms) * (kBToGb / (args.iters * kMsToS)));
      table.push_back(PerfTableRow{size_bytes, false, gbps});
    }

    ccoBarrierAll(ctx.comm);
  }

  ccoBarrierAll(ctx.comm);
  if (my_pe == 0) {
    // Mirror the geometry launch_sdma derives when -c / -T are absent.
    int print_grid = args.nblocks;
    int print_block = args.threads_per_block;
    const char* print_scope = ScopeToChar(args.put_scope);
    if (args.transport == Transport::kSdma) {
      if (!args.nblocks_explicit) print_grid = 1;
      if (!args.threads_explicit) {
        print_block = args.put_scope == PutScope::kWarp ? ctx.device_warp_size
                      : args.put_scope == PutScope::kBlock
                          ? 256
                          : static_cast<int>(ctx.devComm.sdma.sdmaNumQueue);
      }
    }
    PrintPerfTable("p2p_get_bw unidirection", TransportToChar(args.transport), print_scope,
                   print_grid, print_block, ctx.device_warp_size, args.iters, args.warmup,
                   PerfTableMetric::kBandwidthGbps, table);
  }

  if (run_kernels) {
    PerfResFree(&res);
  }
  PerfFinalize(&ctx);
  return 0;
}
